alias a r0
alias b r1
alias c r2
l r4 d0 iron
move b 2
add c a b
#log c

#section: alias
 alias Vending d0
 alias Slot r0
 alias ItemHash r1
 alias ItemNumber r2
 alias ItemCount r3
 alias RequestCode r4
main:
yield
# Очищаем первые 15 ячеек стека, это будет массив со счётчиками
move sp 0
clear:
push 0
blt sp 15 clear
move Slot 2                          # Начинаем перебор слотов со 2 по 101
slotloop:
db
ls ItemHash Vending Slot PrefabHash  # Получаем хэш предмета в очередном слоте
#section: routing
	search:                            # Опорная метка для счётчика
	beqz ItemHash empty                  # Слот пуст - идём в конец цикла
	beqal ItemHash -1057658015 found     # Пшеница
	beqal ItemHash 1924673028 found      # Соевые бобы
	beqal ItemHash 258339687 found       # Кукуруза
	beqal ItemHash 1929046963 found      # Картофель
	beqal ItemHash -998592080 found      # Помидор
	beqal ItemHash 1277828144 found      # Тыква
	beqal ItemHash 658916791 found       # Рис
	beqal ItemHash 2044798572 found      # Гриб
	beqal ItemHash 892110467 found       # Папоротник
	beqal ItemHash -81376085 found       # Красный цветок
	beqal ItemHash -1411986716 found     # Оранжевый цветок
	beqal ItemHash 1712822019 found      # Жёлтый цветок
	beqal ItemHash -1513337058 found     # Зелёный цветок
	beqal ItemHash -1573623434 found     # Синий цветок
	beqal ItemHash 176446172 found       # Инопланетный гриб
s Vending RequestHash ItemHash       # Попался непонятный предмет, выбрасываем
j main                               # Сбились со счёта, начинаем заново
found:                               # Что-то нашли
sub ItemNumber ra search             # Вычисляем позицию строки относительно опорной метки
sub sp ItemNumber 2                  # Вычитаем дополнительное смещение для нужного индекса в стеке
pop ItemCount                        # Берём значение из стека по индексу
add ItemCount ItemCount 1            # Увеличиваем на 1
push ItemCount                       # И записываем обратно в стек
empty:
add Slot Slot 1
blt Slot 102 slotloop                # Переходим к следующему слоту
move sp 15                           # Перебираем массив в стеке, начиная с конца
move RequestCode 0                   # И формируем двоичный код
makecode:
pop ItemCount
slt ItemCount ItemCount 7            # Если число растений на складе меньше 7
add RequestCode RequestCode RequestCode
add RequestCode RequestCode ItemCount # То соответствующий бит кода будет равен 1
bnez sp makecode
s db Setting RequestCode             # Записываем этот код в db для контроллеров гидропоники
j main
